---
layout:      post
title:       Building a Nix package
date:        2022-06-04 15:00:00
updated:     2022-06-04 15:00:00
description: How to package a simple Go application for NixOS.
---

<p>I was looking for the <a href="https://gitlab.com/jtaimisto/bluewalker" title="An open-source command line BLE scanner">bluewalker</a> <abbr title="Bluetooth Low Energy">BLE</abbr> scanner application on NixOS and it wasn’t available yet. A great opportunity to learn how to package a simple command line application in Nix! First, check if the package does not <a href="https://search.nixos.org" title="NixOS Package Search">exist</a> yet. Next, check the <a href="https://github.com/NixOS/nixpkgs/issues" title="NixOS nixpkg issue list @ Github">nixpkgs repo</a> open issues to make sure it’s not already being worked on. Nothing there so let’s give it a shot.</p>

<p>I’ll use my bluewalker package <a href="https://github.com/NixOS/nixpkgs/pull/176242" title="My humble attempt for a first Nix package">pull request</a> as an example.</p>

<p>Clone the NixOS package repository and create a new folder to store the application’s configuration. Add a ‘default.nix’ file in that folder, this is where we’ll add the package’s build instructions.</p>

{% highlight bash %}
git clone --depth 1 git@github.com:nixos/nixpkgs.git /tmp/nixpkgs
mkdir /tmp/nixpkgs/pkgs/tools/bluetooth/bluewalker
touch /tmp/nixpkgs/pkgs/tools/bluetooth/bluewalker/default.nix
{% endhighlight %}

<p>I used an existing Golang config as a base and <a href="https://github.com/NixOS/nixpkgs/blob/60e0a52f11fa55c2a8fcfa47c39441b343aefc73/pkgs/tools/bluetooth/bluewalker/default.nix" title="Build configuration for bluewalker">modified</a> it a bit. Most options make perfect sense, the ldflags are optional but should make the resulting binary a little smaller by removing some debug info.</p>

{% highlight nix %}
{ lib, buildGoModule, fetchFromGitLab }:
buildGoModule rec {
  pname = "bluewalker";
  version = "0.3.0";
  src = fetchFromGitLab {
    owner = "jtaimisto";
    repo = pname;
    rev = "v${version}";
    sha256 = "sha256-spuJRiNiaBV4EsetUq8vUfR6ejUNZxLhVzS3AZZyrrQ=";
  };
  vendorSha256 = "189qs6vmx63vwsjmc4qgf1y8xjsi7x6l1f5c3kd8j8jnagl26z4h";
  ldflags = [
    "-w"
    "-s"
  ];
  meta = with lib; {
    description = "Simple command line Bluetooth LE scanner";
    homepage = "https://gitlab.com/jtaimisto/bluewalker";
    changelog = "https://gitlab.com/jtaimisto/bluewalker/-/tags/v${version}";
    license = licenses.bsd2;
    maintainers = with maintainers; [ cimm ];
    platforms = platforms.linux;
  };
}
{% endhighlight %}
<figcaption>↑ Example default.nix for the bluewalker package.</figcaption>

<p>The configuration includes two <abbr title="Secure Hash Algorithm">SHA</abbr>-256 hashes, which was the most cryptic part at first. Nix will calculate the hash of the downloaded source to make sure it matches what I had while packaging, a security measure. Both hashes need to be calculated and added to the config.</p>

<p>The first, the one in the ‘fetchFromGitLab’ section, is the most complex. To calculate this hash, I downloaded the source code in a temporary folder, making sure to use the exact version with the git tag. I removed the ‘.git’ folder and used the ‘nix hash-path’ command to calculate the hash. I then added the resulting hash to the default.nix configuration.</p>

{% highlight bash %}
git clone --branch "v0.3.0" https://gitlab.com/jtaimisto/bluewalker.git /tmp/bluewalker
rm -r /tmp/bluewalker/.git
nix hash-path /tmp/bluewalker
{% endhighlight %}

<p>Since the bluewalker’s Go modules are not vendored I added the ‘vendorSha256’ hash as well. For this hash I built the package a first time with ‘vendorSha256=lib.fakeSha256;’ as temporary SHA-256 value. The build failed and had the expected hash in the output. I copied the correct SHA-256 hash and built the package again.</p>

{% highlight bash %}
nix-build -E 'with import <nixpkgs> {}; callPackage ./default.nix {}'
{% endhighlight %}

<p>This worked. I could now run ‘/etc/nixos/result/bin/bluewalker -h’ to test if the application worked. Great! Finally, I added the newly built package to my main ‘configuration.nix’.</p>

{% highlight bash %}
environment.systemPackages = [ pkgs.bluewalker ];
{% endhighlight %}

<p>And build NixOS with the local package repository.</p>

{% highlight bash %}
nixos-rebuild switch -I nixpkgs=/tmp/nixpkgs
{% endhighlight %}

<p>Done! Well, almost, since I took the time adding the application, I might as well be a good open-source citizen and contribute the changes back to the main Nix package repository. Who knows, it might even be included so others can install the application more easily. I linked the package in the ‘pkgs/top-level/all-packages.nix’ file and added myself to the ‘maintainers/maintainer-list.nix’ file before opening a new pull request.</p>
